def isIndependent = rootProject.ext.runModule == "customer" //当前module是否正在独立运行

if (isIndependent) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28


    defaultConfig {
        if (isIndependent) {
            applicationId "com.example.aaron.compsapp.customer"
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField("String[]", "MODULE_DEPENDENCIES", getDependenciesStr(rootProject.ext.customerDependencies))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    publishNonDefault true
    defaultPublishConfig "dependentRelease"

    flavorDimensions "dependency"
    productFlavors {
        if (isIndependent) {
            independent {
                dimension "dependency"
                matchingFallbacks = ["dependent"]
            }
        } else {
            dependent {
                dimension "dependency"
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":base")

    //需要依赖的module
    rootProject.ext.customerDependencies.each { v ->
        if (rootProject.ext.runModule != v) {
            //当前运行的module无法依赖
            runtimeOnly project(":$v")
        }
    }
}