def moduleName = project.getName()
def isIndependent = rootProject.ext.runModule == moduleName //当前module是否正在独立运行
def moduleDependencies = rootProject.ext["${moduleName}Dependencies"]

if (isIndependent) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28


    defaultConfig {
        if (isIndependent) {
            applicationId "${rootProject.ext.applicationId}.${moduleName}"
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField("String[]", "MODULE_DEPENDENCIES", getDependenciesStr(moduleDependencies))

        //其他组件module依旧需要引入
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    publishNonDefault true
    defaultPublishConfig "dependentRelease"

    flavorDimensions "dependency"
    productFlavors {
        if (isIndependent) {
            independent {
                dimension "dependency"
                matchingFallbacks = ["dependent"]
            }
        } else {
            dependent {
                dimension "dependency"
            }
        }
    }

    sourceSets {
        main {
//            if (isIndependent) {
//                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
//            } else {
//                manifest.srcFile 'src/main/release/AndroidManifest.xml'
//            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":base")

    //需要依赖的module
    moduleDependencies.each { v ->
        if (rootProject.ext.runModule != v) {
            //当前运行的module无法依赖
            runtimeOnly project(":$v")
        }
    }

//    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2' //其他组件module依旧需要引入
    kapt "com.alibaba:arouter-compiler:$rootProject.ext.arouter_compiler_version" //其他组件module依旧需要引入
}