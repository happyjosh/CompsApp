//def isIndependent = !rootProject.ext.runDependencies.contains("task")
def isIndependent = rootProject.ext.runModule == "setting" //当前module是否正在独立运行

if (isIndependent) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28


    defaultConfig {
        if (isIndependent) {
            applicationId "com.example.aaron.compsapp.setting"
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    publishNonDefault true
    defaultPublishConfig "dependentRelease"

    flavorDimensions "dependency"
    productFlavors {
        if (isIndependent) {
            independent {
                dimension "dependency"
                matchingFallbacks = ["dependent"]
            }
        } else {
            dependent {
                dimension "dependency"
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":base")

    //需要依赖的module
    rootProject.ext.settingDependencies.each { v ->
        if (rootProject.ext.runModule != v) {
            //当前运行的module无法依赖
            implementation project(":$v")
        }
    }
}